buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.2'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.8'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.+"
    }
}

plugins {
    id 'java'
    id 'com.gradle.build-scan' version '2.1'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'net.saliman.properties' version '1.4.6'
}

apply from: 'gradle/doc.gradle'

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishOnFailure()
}

allprojects {
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'idea'
    apply plugin: 'net.saliman.properties'

    ext {
        proteusVersion = '1.6.0'
        springBootDependenciesVersion = '2.1.3.RELEASE'
        proteusCommonDependenciesVersion = '1.6.0'
    }

    project.version += project.hasProperty('versionSuffix') ? project.property('versionSuffix') : ''

    repositories {
        jcenter()
        maven {
            url  "https://dl.bintray.com/netifi/netifi-oss"
        }
        maven {
            url  "https://dl.bintray.com/rsocket-admin/RSocket"
        }

        maven {
            url = 'https://artifactory.netifiinc.com/artifactory/libs-release-local'
            credentials {
                username = project.hasProperty('netifiArtifactoryUsername') ? project.property('netifiArtifactoryUsername') : System.getenv('NETIFI_ARTIFACTORY_USERNAME')
                password = project.hasProperty('netifiArtifactoryPassword') ? project.property('netifiArtifactoryPassword') : System.getenv('NETIFI_ARTIFACTORY_PASSWORD')
            }
        }

        maven {
            url = 'https://artifactory.netifiinc.com/artifactory/libs-snapshot-local'
            credentials {
                username = project.hasProperty('netifiArtifactoryUsername') ? project.property('netifiArtifactoryUsername') : System.getenv('NETIFI_ARTIFACTORY_USERNAME')
                password = project.hasProperty('netifiArtifactoryPassword') ? project.property('netifiArtifactoryPassword') : System.getenv('NETIFI_ARTIFACTORY_PASSWORD')
            }
        }
    }

    license {
        header rootProject.file('codequality/HEADER')
        strictCheck true
    }

    test {
        testLogging {
            events "passed", "skipped", "failed"
        }
        useJUnitPlatform()
    }
}
